# Maintainer: Jonne Ha√ü <me@mrzyx.de>
pkgbase=diaspora-git
pkgname=diaspora-git
_projectname=diaspora
true && pkgname=('diaspora-mysql-git' 'diaspora-postgresql-git')
pkgver=0.1.1.0.42.gcdc29e4
pkgrel=1
pkgdesc="A distributed privacy aware social network (development head)"
arch=('i686' 'x86_64')
url="http://diasporafoundation.org"
license=('AGPL3')
depends=('ruby1.9' 'ruby1.9-bundler' 'redis' 'imagemagick' 'libxslt' 'net-tools')
makedepends=('git' 'libmysqlclient' 'postgresql-libs' 'nodejs')
conflicts=('diaspora' 'diaspora-mysql' 'diaspora-postgresql')
options=(!strip)
backup=("etc/webapps/$_projectname/diaspora.yml"
        "etc/webapps/$_projectname/database.yml"
        "etc/webapps/$_projectname/secret_token.rb")
install="$_projectname.install"
source=("git://github.com/$_projectname/$_projectname.git#branch=develop"
        "$_projectname.install"
        "$_projectname.service"
        "$_projectname.bashrc")

pkgver() {
  cd $srcdir/$_projectname
  git describe --always | sed -e 's|-|.|g' -e 's|^v||'
}

# Get rid of any possible ruby version managers
# From https://github.com/postmodern/chruby
_reset_ruby() {
  [[ -z "$RUBY_ROOT" ]] && return

  export PATH=":$PATH:"; export PATH=${PATH//:$RUBY_ROOT\/bin:/:}

  [[ -n "$GEM_HOME" ]] && export PATH=${PATH//:$GEM_HOME\/bin:/:}
  [[ -n "$GEM_ROOT" ]] && export PATH=${PATH//:$GEM_ROOT\/bin:/:}

  export GEM_PATH=":$GEM_PATH:"
  export GEM_PATH=${GEM_PATH//:$GEM_HOME:/:}
  export GEM_PATH=${GEM_PATH//:$GEM_ROOT:/:}
  export GEM_PATH=${GEM_PATH#:}; export GEM_PATH=${GEM_PATH%:}
  unset GEM_ROOT GEM_HOME

  export PATH=${PATH#:}; export PATH=${PATH%:}
  unset RUBY_ROOT RUBY_ENGINE RUBY_VERSION RUBYOPT
}

_package() {
  _bundle=bundle-1.9
  _ruby=ruby-1.9
  _rake=rake-1.9
  _db=$1
  _srcdir=$srcdir/$pkgname-$pkgver

  _reset_ruby

  msg "Setup build directory"
  mkdir -p $_srcdir
  cp -Rf $srcdir/$_projectname/{app,config,db,public,lib,script,vendor,config.ru,Gemfile,Gemfile.lock,Procfile,Rakefile} $_srcdir

  cd $_srcdir

  msg "Bundle dependencies"
  echo "gem: --no-rdoc --no-ri --no-user-install" > $_srcdir/.gemrc
  HOME=$_srcdir DB=$_db $_bundle install \
    --without development test heroku --path vendor/
  HOME=$_srcdir DB=$_db $_bundle clean

  msg "Patch configuration examples"
  sed -i -e "s|#certificate_authorities: '/etc/ssl/certs/ca-certificates.crt'|certificate_authorities: '/etc/ssl/certs/ca-certificates.crt'|" \
         -e "s|#rails_environment: 'production'|rails_environment: 'production'|" \
         -e "s|#database: 'mysql'|database: '$_db'|" \
      $_srcdir/config/diaspora.yml.example
  sed -i -e "s|<<: \*mysql|<<: *$_db|" \
         -e "s|#<<: \*postgres||" \
      $_srcdir/config/database.yml.example

  cp $_srcdir/config/{diaspora.yml.example,diaspora.yml}

  msg "Create secret token"
  HOME=$_srcdir DB=$_db $_bundle exec $_rake generate:secret_token

  msg "Precompile assets"
  DB=$_db $_bundle exec $_rake assets:precompile

  rm $_srcdir/config/diaspora.yml

  msg "Copy contents to package directory"
  install -dm755 $pkgdir/usr/share/webapps/$_projectname
  cp -Rf $_srcdir/* $pkgdir/usr/share/webapps/$_projectname/
  cp -Rf $_srcdir/.bundle $pkgdir/usr/share/webapps/$_projectname/
  install -Dm644 $_srcdir/.gemrc $pkgdir/usr/share/webapps/$_projectname/.gemrc
  install -Dm640 $_srcdir/config/initializers/secret_token.rb $pkgdir/etc/webapps/$_projectname/secret_token.rb
  install -Dm644 $srcdir/$_projectname.service $pkgdir/usr/lib/systemd/system/$_projectname.service
  install -Dm644 $srcdir/$_projectname.bashrc  $pkgdir/usr/share/webapps/$_projectname/.bashrc

  msg "Build source.tar.gz to conform the AGPL"
  tar czf $pkgdir/usr/share/webapps/$_projectname/public/source.tar.gz \
          $pkgdir/usr/share/webapps/$_projectname/{app,db,lib,script,Gemfile,Gemfile.lock,Rakefile,Procfile,config.ru}

  msg "Symlink ruby and bundle"
  install -dm755          $pkgdir/usr/share/webapps/$_projectname/bin
  ln -s /usr/bin/$_ruby   $pkgdir/usr/share/webapps/$_projectname/bin/ruby
  ln -s /usr/bin/$_bundle $pkgdir/usr/share/webapps/$_projectname/bin/bundle

  msg "Prepare configuration files"
  install -dm750 $pkgdir/etc/webapps/$_projectname
  install -Dm640 $_srcdir/config/diaspora.yml.example $pkgdir/etc/webapps/$_projectname/diaspora.yml
  install -Dm640 $_srcdir/config/database.yml.example $pkgdir/etc/webapps/$_projectname/database.yml

  sed -i -e "s|%db%|$_db|" \
      $pkgdir/usr/share/webapps/$_projectname/.bashrc
  sed -i -e "s|%db%|$_db|" \
         -e "s|mysql|mysqld|" \
         -e "s|postgres|postgresql|" \
      $pkgdir/usr/lib/systemd/system/$_projectname.service

  msg "Create symlinks"
  install -dm755 $pkgdir/var/log/$_projectname
  install -dm755 $pkgdir/tmp/$_projectname
  install -dm755 $pkgdir/var/lib/$_projectname/uploads
  rm -Rf $pkgdir/usr/share/webapps/$_projectname/log \
         $pkgdir/usr/share/webapps/$_projectname/tmp \
         $pkgdir/usr/share/webapps/$_projectname/public/uploads
  ln -s  /etc/webapps/$_projectname/diaspora.yml    $pkgdir/usr/share/webapps/$_projectname/config/diaspora.yml
  ln -s  /etc/webapps/$_projectname/database.yml    $pkgdir/usr/share/webapps/$_projectname/config/database.yml
  ln -sf /etc/webapps/$_projectname/secret_token.rb $pkgdir/usr/share/webapps/$_projectname/config/initializers/secret_token.rb
  ln -sf /var/lib/$_projectname/uploads             $pkgdir/usr/share/webapps/$_projectname/public/uploads
  ln -sf /tmp/$_projectname                         $pkgdir/usr/share/webapps/$_projectname/tmp
  ln -sf /var/log/$_projectname                     $pkgdir/usr/share/webapps/$_projectname/log
}

package_diaspora-mysql-git() {
  true && pkgdesc="$pkgdesc (MySQL)"
  true && depends=(${depends[@]} 'libmysqlclient' 'mysql')
  true && conflicts=(${conflicts[@]} 'diaspora-postgresql-git')

  _package mysql
}

package_diaspora-postgresql-git() {
  true && pkgdesc="$pkgdesc (PostgreSQL)"
  true && depends=(${depends[@]} 'postgresql-libs' 'postgresql')
  true && conflicts=(${conflicts[@]} 'diaspora-mysql-git')

  _package postgres
}

sha256sums=('SKIP'
            '4a8154ded485c8ef6ee657217cadeac9604715f9cc5dac866901bb4c778582f6'
            '06d6d6b786abbb5126a311da1c64cc7004683875f4ecfd7c2735c584987638a7'
            'a3e17221e8cdbe5a8073f4725960f794c31208b0098177a296356af293b14596')
