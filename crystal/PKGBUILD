# Maintainer: Jonne Ha√ü <me@jhass.eu>
pkgname=crystal
pkgver=0.7.1
pkgrel=1
pkgdesc="The Crystal Programming Language"
arch=('i686' 'x86_64')
url="http://crystal-lang.org"
license=('Apache')
depends=('libunwind' 'gc' 'llvm' 'libatomic_ops' 'pcre' 'libpcl' 'libevent')
checkdepends=('libyaml' 'libxml2')
optdepends=('libyaml: For YAML support'
            'gmp: For BigInt support'
            'libxml2: For XML support')
conflicts=('crystal-git')
source=("https://github.com/manastech/crystal/archive/$pkgver.tar.gz")
source_i686+=("http://cloud.aeshna.de/u/mrzyx/crystal32/crystal32-$pkgver.tar.gz")
source_x86_64+=("https://github.com/manastech/crystal/releases/download/$pkgver/$pkgname-$pkgver-1-linux-x86_64.tar.gz")

prepare() {
  rm -rf "$srcdir/bin"

  if [ -d "$srcdir/$pkgname-$pkgver-1/bin" ]; then
    ln -sf "$srcdir/$pkgname-$pkgver-1/bin" "$srcdir/bin"
  elif [ -f "$srcdir/crystal32-$pkgver" ]; then
    mkdir -p "$srcdir/bin"
    cp "$srcdir/crystal32-$pkgver" "$srcdir/bin/crystal"
  fi
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  make release=1 \
       PATH="$srcdir/bin:$PATH" \
       CRYSTAL_PATH="$srcdir/$pkgname-$pkgver/src" \
       CRYSTAL_CONFIG_VERSION="$pkgver" \
       CRYSTAL_CONFIG_PATH="libs:/usr/lib/crystal"
}

check() {
  cd "$srcdir/$pkgname-$pkgver"

  make spec CRYSTAL_PATH="$srcdir/$pkgname-$pkgver/src" \
            CRYSTAL_CONFIG_VERSION="$pkgver"
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  # /usr/bin/crystal compiled executable
  # /usr/lib/crystal/ compiler src & core libs
  # /usr/share/doc/crystal/samples/ samples

  install -Dm755 ".build/crystal" "$pkgdir/usr/bin/crystal"
  install -dm755 "$pkgdir/usr/lib"
  cp -rv src "$pkgdir/usr/lib/crystal"
  install -dm755 "$pkgdir/usr/share/doc/crystal"
  cp -rv samples "$pkgdir/usr/share/doc/crystal/"
}
sha256sums=('06cce25175672a67e536d36d5cb3908e1645c967e9cbd5add1f948aed95c5d59')
sha256sums_i686=('9a36f2281638beb71af3d2af4c47b0b2875ea2fe893d30c3517b18107684eff1')
sha256sums_x86_64=('9f7ac454dddcc1562eda7bc1ae507ab71fd063683ecc2c64ddca10287d252cbc')
