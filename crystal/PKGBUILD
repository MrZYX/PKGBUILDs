# Maintainer: Jonne Ha√ü <me@jhass.eu>
pkgname=crystal
pkgver=0.5.9
pkgrel=1
pkgdesc="The Crystal Programming Language"
arch=('i686' 'x86_64')
url="http://crystal-lang.org"
license=('Apache')
depends=('libunwind' 'gc' 'llvm' 'libatomic_ops' 'pcre')
checkdepends=('libyaml' 'libxml2')
optdepends=('libyaml: For YAML support'
            'gmp: For BigInt support'
            'libpcl: For coroutine (Fiber) support'
            'libxml2: For XML support')
conflicts=('crystal-git')
source=("https://github.com/manastech/crystal/archive/$pkgver.tar.gz")
source_i686+=("http://cloud.aeshna.de/u/mrzyx/crystal32/crystal32-$pkgver.tar.gz")
source_x86_64+=("https://github.com/manastech/crystal/releases/download/$pkgver/$pkgname-$pkgver-1-linux-x86_64.tar.gz")

prepare() {
  rm -rf "$srcdir/bin"

  if [ -d "$srcdir/$pkgname-$pkgver-1/bin" ]; then
    ln -sf "$srcdir/$pkgname-$pkgver-1/bin" "$srcdir/bin"
  elif [ -f "$srcdir/crystal32-$pkgver" ]; then
    mkdir -p "$srcdir/bin"
    cp "$srcdir/crystal32-$pkgver" "$srcdir/bin/crystal"
  fi
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  make release=1 \
       PATH="$PATH:$srcdir/bin" \
       CRYSTAL_PATH="$srcdir/$pkgname-$pkgver/src" \
       CRYSTAL_CONFIG_VERSION="$pkgver" \
       CRYSTAL_CONFIG_PATH="libs:/usr/lib/crystal"
}

check() {
  cd "$srcdir/$pkgname-$pkgver"

  make spec CRYSTAL_PATH="$srcdir/$pkgname-$pkgver/src" \
            CRYSTAL_CONFIG_VERSION="$pkgver"
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  # /usr/bin/crystal compiled executable
  # /usr/lib/crystal/ compiler src & core libs
  # /usr/share/doc/crystal/samples/ samples

  install -Dm755 ".build/crystal" "$pkgdir/usr/bin/crystal"
  install -dm755 "$pkgdir/usr/lib"
  cp -rv src "$pkgdir/usr/lib/crystal"
  install -dm755 "$pkgdir/usr/share/doc/crystal"
  cp -rv samples "$pkgdir/usr/share/doc/crystal/"
}
sha256sums=('0f509d60b64ac89fc41845ed86eb649782e58a0fff984069a38dcbc5a4d9bd78')
sha256sums_i686=('267f69f0d8e48f8dc4569fb0d043f25d2bdb78b1f5035db6aeb07472a940e1c7')
sha256sums_x86_64=('cb7ef35e475736eae553affeff161b448d2a3ed2d0e8472a6c2ad9842fbfa8a3')
