# Maintainer: Jonne Ha√ü <me@mrzyx.de>
pkgname=crystal
pkgver=0.5.0
pkgrel=2
pkgdesc="The Crystal Programming Language"
arch=('x86_64')
url="http://crystal-lang.org"
license=('Apache')
depends=('libunwind' 'gc' 'llvm' 'libatomic_ops' 'pcre')
checkdepends=('libyaml')
optdepends=('libyaml: For YAML support'
            'gmp: For BigInt support'
            'libpcl: For coroutine (Fiber) support')
conflicts=('crystal-git')
source=("https://github.com/manastech/crystal/releases/download/$pkgver/$pkgname-$pkgver-1-linux-x86_64.tar.gz"
        "$pkgver.tar.gz::https://github.com/manastech/crystal/archive/master.tar.gz"
        "crystal.sh")

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make release=1 \
       PATH="$PATH:$srcdir/$pkgname-$pkgver-1/bin" \
       CRYSTAL_CONFIG_VERSION="$pkgver" \
       CRYSTAL_CONFIG_PATH="/usr/lib/crystal/src:/usr/lib/crystal/libs"
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make spec PATH="$PATH:$srcdir/$pkgname-$pkgver-1/bin"
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  # /usr/bin/crystal wrapper script ensuring right environment, ideally the executable would have compiled in defaults. Maybe provide profile.d file instead
  # /usr/lib/crystal/crystal compiled executable
  # /usr/lib/crystal/src compiler src & core libs
  # /usr/lib/cystal/libs stdlib
  # /usr/share/doc/crystal/samples samples
  install -Dm755 "$srcdir/crystal.sh" "$pkgdir/usr/bin/crystal"
  install -dm755 "$pkgdir/usr/lib/crystal"
  install -Dm755 ".build/crystal" "$pkgdir/usr/lib/crystal/crystal"
  cp -rv src "$pkgdir/usr/lib/crystal/"
  cp -rv libs "$pkgdir/usr/lib/crystal/"
  install -dm755 "$pkgdir/usr/share/doc/crystal"
  cp -rv samples "$pkgdir/usr/share/doc/crystal/"
}
sha256sums=('17071df3c7b975463591778b5baeb652a40aa539635668be1b4391857bcc1d8d'
            '95b02c72368158303e41f1be20ab1e01ff9526993c48d37abd6e869f39688dd9'
            '5fef4c439537a3b29803fce6a064a4d07b079bcdb6980b52e3ec547a86d191c4')
