# Maintainer: Jonne Ha√ü <me@mrzyx.de>
pkgname=crystal
pkgver=0.5.1
pkgrel=1
pkgdesc="The Crystal Programming Language"
arch=('x86_64')
url="http://crystal-lang.org"
license=('Apache')
depends=('libunwind' 'gc' 'llvm' 'libatomic_ops' 'pcre')
checkdepends=('libyaml')
optdepends=('libyaml: For YAML support'
            'gmp: For BigInt support'
            'libpcl: For coroutine (Fiber) support')
conflicts=('crystal-git')
source=("https://github.com/manastech/crystal/releases/download/$pkgver/$pkgname-$pkgver-1-linux-x86_64.tar.gz"
        "https://github.com/manastech/crystal/archive/$pkgver.tar.gz"
        "crystal.sh")

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make release=1 \
       PATH="$PATH:$srcdir/$pkgname-$pkgver-1/bin" \
       CRYSTAL_PATH="$srcdir/$pkgname-$pkgver/src:$srcdir/$pkgname-$pkgver/libs" \
       CRYSTAL_CONFIG_VERSION="$pkgver" \
       CRYSTAL_CONFIG_PATH="/usr/lib/crystal/src:/usr/lib/crystal/libs"
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make spec PATH="$PATH:$srcdir/$pkgname-$pkgver-1/bin" \
            CRYSTAL_PATH="$srcdir/$pkgname-$pkgver/src:$srcdir/$pkgname-$pkgver/libs" \
            CRYSTAL_CONFIG_VERSION="$pkgver"
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  # /usr/bin/crystal wrapper script ensuring right environment, ideally the executable would have compiled in defaults. Maybe provide profile.d file instead
  # /usr/lib/crystal/crystal compiled executable
  # /usr/lib/crystal/src compiler src & core libs
  # /usr/lib/cystal/libs stdlib
  # /usr/share/doc/crystal/samples samples
  install -Dm755 "$srcdir/crystal.sh" "$pkgdir/usr/bin/crystal"
  install -dm755 "$pkgdir/usr/lib/crystal"
  install -Dm755 ".build/crystal" "$pkgdir/usr/lib/crystal/crystal"
  cp -rv src "$pkgdir/usr/lib/crystal/"
  cp -rv libs "$pkgdir/usr/lib/crystal/"
  install -dm755 "$pkgdir/usr/share/doc/crystal"
  cp -rv samples "$pkgdir/usr/share/doc/crystal/"
}
sha256sums=('d826641b28c486969b3e65136be8636d1619b4d3897add21230f65f0916f1ca0'
            '3506610572f94730f1fe7d852bcf397f5a64b9ddadff83ed42a397d011c810a0'
            '5fef4c439537a3b29803fce6a064a4d07b079bcdb6980b52e3ec547a86d191c4')
